' Gambas class file


Public Sub Form_Open()

  repo_check()
  app_check("dnsminder", LaunchDNSMinderButton, InstallDNSMinderButton, UninstallDNSMinderButton)
  DNSMinderSpinner.Show

End

Sub repo_check()
    
  If Not Exist("/etc/apt/sources.list.d/ubuntuce-jammy.list") Then
    If Message.Warning("The UbuntuCE Repo is not installed. \n\n It is required! \n\n Would you like to install it now?", "Install Now", "Cancel") = 1 Then 
      Exec ["gnome-terminal", "--", "bash", "-c", "wget https://job.ubuntuce.com/KEY.gpg -O KEY.gpg && gpg --output ubuntuce.gpg --dearmor KEY.gpg && sudo mv ubuntuce.gpg /usr/share/keyrings/ && rm KEY.gpg && sudo apt install curl -y && sudo curl -s --compressed -o /etc/apt/sources.list.d/ubuntuce-jammy.list 'https://job.ubuntuce.com/ubuntuce-jammy.list' && sudo apt update"]
    Else 
      Quit
    Endif
  Endif
  
End

Sub app_check(appName As String, launchButton As Button, installButton As Button, uninstallButton As Button)
  
  Dim CheckProcess As Process
  
  CheckProcess = Shell ("apt-cache policy " & appName & " | grep 'none' | awk '{print $2}'") 
  
  Do
    Try Wait 0.5    
  Loop Until CheckProcess.State = 0
  
  If InStr(Str(CheckProcess.Value), "(none)\n") = 0 Then 
    launchButton.Hide
    installButton.Show
    uninstallButton.Hide
  Else 
    launchButton.Show
    installButton.Hide
    uninstallButton.Show
  Endif
  
  CheckProcess = Null
  
End

Sub install_app(appName As String, installSpinner As Spinner, launchButton As Button, installButton As Button, uninstallButton As Button)

  Dim InstallProcess As Process
  
  If Message.Question("Are you sure?") = 1 Then 
    InstallProcess = TerminalView.Shell("pkexec apt install " & appName & " -y")
    
    installSpinner.Show
    launchButton.Hide
    installButton.Show
    uninstallButton.Hide
    Do
      Try Wait 0.5    
    Loop Until InstallProcess.State = 0
  
    launchButton.Show
    installButton.Hide
    uninstallButton.Show
    installSpinner.Hide
    
    InstallProcess = Null
      
  Endif
  
End


Sub launch_app(appName As String, Optional isTerminal As Boolean)
  
  If isTerminal Then 
    Exec ["gnome-terminal", "--", "bash", "-c", appName]
  Else 
    Exec [appName]
  Endif
  
End


Public Sub LaunchDNSMinderButton_Click()

  launch_app("dnsminder")

End

Public Sub InstallDNSMinderButton_Click()

  install_app("dnsminder", DNSMinderSpinner, LaunchDNSMinderButton, InstallDNSMinderButton, UninstallDNSMinderButton)

End
