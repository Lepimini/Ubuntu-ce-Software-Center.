' Gambas class file


Public Sub Form_Open()

  repo_check()
  app_check("dnsminder", LaunchDNSMinderButton, InstallDNSMinderButton, UninstallDNSMinderButton)
  app_check("hostminder", LaunchHostMinderButton, InstallHostMinderButton, UninstallHostMinderButton)
  app_check("ubuntu-ce-parental-controls", LaunchParentalControlsButton, InstallParentalControlsButton, UninstallParentalControlsButton)
  app_check("timekpr-next", LaunchTimekprButton, InstallTimekprButton, UninstallTimekprButton)

End

Sub repo_check()
    
  If Not Exist("/etc/apt/sources.list.d/ubuntuce-jammy.list") Then
    If Message.Warning("The UbuntuCE Repo is not installed. \n\n It is required! \n\n Would you like to install it now?", "Install Now", "Cancel") = 1 Then 
      Exec ["gnome-terminal", "--", "bash", "-c", "wget https://job.ubuntuce.com/KEY.gpg -O KEY.gpg && gpg --output ubuntuce.gpg --dearmor KEY.gpg && sudo mv ubuntuce.gpg /usr/share/keyrings/ && rm KEY.gpg && sudo apt install curl -y && sudo curl -s --compressed -o /etc/apt/sources.list.d/ubuntuce-jammy.list 'https://job.ubuntuce.com/ubuntuce-jammy.list' && sudo apt update"]
    Else 
      Quit
    Endif
  Endif
  
End

Sub app_check(appName As String, launchButton As Button, installButton As Button, uninstallButton As Button)
  
  Dim Result As String
  
  Shell ("apt-cache policy " & appName & " | grep 'none' | awk '{print $2}'") To Result
   
  If InStr(Result, "(none)\n") = 1 Then 
    launchButton.Hide
    installButton.Show
    uninstallButton.Hide
  Else 
    launchButton.Show
    installButton.Hide
    uninstallButton.Show
  Endif
  
  
End

Sub install_app(appName As String, installSpinner As Spinner, launchButton As Button, installButton As Button, uninstallButton As Button)

  Dim InstallProcess As Process
  
  If Message.Question("Are you sure?") = 1 Then 
    InstallProcess = TerminalView.Shell("pkexec apt install " & appName & " -y")
    
    ' BlockPanel.Show
    TerminalView.Clear
    installSpinner.Start
    installButton.Enabled = False
    Do
      Try Wait 0.5    
    Loop Until InstallProcess.State = 0
  
    launchButton.Show
    launchButton.Enabled = True
    uninstallButton.Show
    uninstallButton.Enabled = True
    installButton.Hide
    installSpinner.Stop
    ' BlockPanel.Hide
    
    InstallProcess = Null
      
  Endif
  
End

Sub uninstall_app(appName As String, installSpinner As Spinner, launchButton As Button, installButton As Button, uninstallButton As Button)

  Dim UninstallProcess As Process
  
  If Message.Question("Are you sure?") = 1 Then 
    UninstallProcess = TerminalView.Shell("pkexec apt autoremove " & appName & " -y")
    
    ' BlockPanel.Show
    TerminalView.Clear
    installSpinner.Start
    uninstallButton.Enabled = False
    launchButton.Enabled = False
    Do
      Try Wait 0.5    
    Loop Until UninstallProcess.State = 0
  
    launchButton.Hide
    installButton.Show
    installButton.Enabled = True
    uninstallButton.Hide
    installSpinner.Stop
    ' BlockPanel.Hide
    
    UninstallProcess = Null
      
  Endif
  
End


Sub launch_app(appName As String, Optional isTerminal As Boolean, Optional isSudo As Boolean)
  
  If isTerminal Then 
    Exec ["gnome-terminal", "--", "bash", "-c", appName]
  Else 
    If isSudo Then 
      Shell (appName)
    Else 
      Exec [appName]
    Endif
  Endif
  
End


Public Sub LaunchDNSMinderButton_Click()

  launch_app("dnsminder")

End

Public Sub InstallDNSMinderButton_Click()

  install_app("dnsminder", DNSMinderSpinner, LaunchDNSMinderButton, InstallDNSMinderButton, UninstallDNSMinderButton)

End

Public Sub UninstallDNSMinderButton_Click()

  uninstall_app("dnsminder", DNSMinderSpinner, LaunchDNSMinderButton, InstallDNSMinderButton, UninstallDNSMinderButton)

End

Public Sub LaunchHostMinderButton_Click()

  launch_app("hostminder")

End

Public Sub InstallHostMinderButton_Click()

  install_app("hostminder", HostMinderSpinner, LaunchHostMinderButton, InstallHostMinderButton, UninstallHostMinderButton)

End

Public Sub UninstallHostMinderButton_Click()

  uninstall_app("hostminder", HostMinderSpinner, LaunchHostMinderButton, InstallHostMinderButton, UninstallHostMinderButton)

End

Public Sub LaunchParentalControlsButton_Click()

  launch_app("malcontent-control")

End

Public Sub InstallParentalControlsButton_Click()

  install_app("ubuntu-ce-parental-controls", ParentalControlsSpinner, LaunchParentalControlsButton, InstallParentalControlsButton, UninstallParentalControlsButton)

End

Public Sub UninstallParentalControlsButton_Click()

  uninstall_app("ubuntu-ce-parental-controls", ParentalControlsSpinner, LaunchParentalControlsButton, InstallParentalControlsButton, UninstallParentalControlsButton)

End

Public Sub LaunchTimekprButton_Click()

  launch_app("pkexec /usr/bin/timekpra", False, True)

End

Public Sub InstallTimekprButton_Click()

  install_app("timekpr-next", TimekprSpinner, LaunchTimekprButton, InstallTimekprButton, UninstallTimekprButton)

End

Public Sub UninstallTimekprButton_Click()

  uninstall_app("timekpr-next", TimekprSpinner, LaunchTimekprButton, InstallTimekprButton, UninstallTimekprButton)

End

Public Sub ShowDetailsButton_Click()

  FMain.H = 554
  ShowDetailsButton.Hide
  HideDetailsButton.Show

End

Public Sub HideDetailsButton_Click()

  FMain.H = 344
  HideDetailsButton.Hide
  ShowDetailsButton.Show

End

