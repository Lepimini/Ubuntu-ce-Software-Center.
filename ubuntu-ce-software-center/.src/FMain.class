' Gambas class file


Public Sub Form_Open()

  repo_check()
  snap_check()
  app_check("dnsminder", LaunchDNSMinderButton, InstallDNSMinderButton, UninstallDNSMinderButton)
  app_check("hostminder", LaunchHostMinderButton, InstallHostMinderButton, UninstallHostMinderButton)
  app_check("ubuntu-ce-parental-controls", LaunchParentalControlsButton, InstallParentalControlsButton, UninstallParentalControlsButton)
  app_check("timekpr-next", LaunchTimekprButton, InstallTimekprButton, UninstallTimekprButton)
  app_check("xiphos", LaunchXiphosButton, InstallXiphosButton, UninstallXiphosButton)
  app_check("bibletime", LaunchBibleTimeButton, InstallBibleTimeButton, UninstallBibleTimeButton)
  app_check("bibledit", LaunchBibleditButton, InstallBibleditButton, UninstallBibleditButton)
  app_check("bible-kjv", LaunchBibleKJVButton, InstallBibleKJVButton, UninstallBibleKJVButton) 
  app_check("bolls", LaunchBollsButton, InstallBollsBibleButton, UninstallBollsButton, True) 

End

Sub repo_check()
    
  If Not Exist("/etc/apt/sources.list.d/ubuntuce-jammy.list") Then
    If Message.Warning("The UbuntuCE Repo is not installed. \n\n It is required! \n\n Would you like to install it now?", "Install Now", "Cancel") = 1 Then 
      Exec ["gnome-terminal", "--", "bash", "-c", "wget https://job.ubuntuce.com/KEY.gpg -O KEY.gpg && gpg --output ubuntuce.gpg --dearmor KEY.gpg && sudo mv ubuntuce.gpg /usr/share/keyrings/ && rm KEY.gpg && sudo apt install curl -y && sudo curl -s --compressed -o /etc/apt/sources.list.d/ubuntuce-jammy.list 'https://job.ubuntuce.com/ubuntuce-jammy.list' && sudo apt update"]
    Else 
      Quit
    Endif
  Endif
  
End

Sub snap_check()
  
  Dim Result As String
  Dim InstallProcess As Process
  
  Shell ("apt-cache policy snapd | grep 'none' | awk '{print $2}'") To Result
  
  If InStr(Result, "(none)") = 1 Then 
    If Message.Warning("The Snapd package is not installed. \n\n It is required! \n\n Would you like to install it now?", "Install Now", "Cancel") = 1 Then 
      InstallProcess = TerminalView.Shell("pkexec apt install snapd -y")
      
      BlockPanel.Show
      TerminalView.Clear

      Do
        Try Wait 0.5    
      Loop Until InstallProcess.State = 0
    
      BlockPanel.Hide
      
      InstallProcess = Null
      
    Else 
      Quit
    Endif
  Endif
    
End


Sub app_check(appName As String, launchButton As Button, installButton As Button, uninstallButton As Button, Optional isSnapd As Boolean)
  
  Dim Result As String
  
  If isSnapd Then 
    Shell ("snap list | grep " & appName & " | awk '{print $1}'") To Result
    
    If InStr(Result, appName) = 0 Then 
      launchButton.Hide
      installButton.Show
      uninstallButton.Hide
    Else 
      launchButton.Show
      installButton.Hide
      uninstallButton.Show
    Endif
  Else 
    Shell ("apt-cache policy " & appName & " | grep 'none' | awk '{print $2}'") To Result
    
    If InStr(Result, "(none)") = 1 Then 
      launchButton.Hide
      installButton.Show
      uninstallButton.Hide
    Else 
      launchButton.Show
      installButton.Hide
      uninstallButton.Show
    Endif
  Endif

  
  
End

Sub install_app(appName As String, installSpinner As Spinner, launchButton As Button, installButton As Button, uninstallButton As Button, Optional isSnapd As Boolean)

  Dim InstallProcess As Process
  
  If Message.Question("Are you sure?") = 1 Then 
    If isSnapd Then 
      InstallProcess = TerminalView.Shell("pkexec snap install " & appName)
    Else 
      InstallProcess = TerminalView.Shell("pkexec apt install " & appName & " -y")
    Endif
    
    BlockPanel.Show
    TerminalView.Clear
    installSpinner.Start
    installButton.Enabled = False
    Do
      Try Wait 0.5    
    Loop Until InstallProcess.State = 0
  
    launchButton.Show
    launchButton.Enabled = True
    uninstallButton.Show
    uninstallButton.Enabled = True
    installButton.Hide
    installSpinner.Stop
    BlockPanel.Hide
    
    InstallProcess = Null
      
  Endif
  
End

Sub uninstall_app(appName As String, installSpinner As Spinner, launchButton As Button, installButton As Button, uninstallButton As Button, Optional isSnapd As Boolean)

  Dim UninstallProcess As Process
  
  If Message.Question("Are you sure?") = 1 Then 
    If isSnapd Then 
      UninstallProcess = TerminalView.Shell("pkexec snap remove " & appName)
    Else 
      UninstallProcess = TerminalView.Shell("pkexec apt autoremove " & appName & " -y")
    Endif
    
    BlockPanel.Show
    TerminalView.Clear
    installSpinner.Start
    uninstallButton.Enabled = False
    launchButton.Enabled = False
    Do
      Try Wait 0.5    
    Loop Until UninstallProcess.State = 0
  
    launchButton.Hide
    installButton.Show
    installButton.Enabled = True
    uninstallButton.Hide
    installSpinner.Stop
    BlockPanel.Hide
    
    UninstallProcess = Null
      
  Endif
  
End


Sub launch_app(appName As String, Optional isTerminal As Boolean, Optional isSudo As Boolean)
  
  If isTerminal Then 
    Exec ["gnome-terminal", "--", "bash", "-c", appName]
  Else 
    If isSudo Then 
      Shell (appName)
    Else 
      Exec [appName]
    Endif
  Endif
  
End


Public Sub LaunchDNSMinderButton_Click()

  launch_app("dnsminder")

End

Public Sub InstallDNSMinderButton_Click()

  install_app("dnsminder", DNSMinderSpinner, LaunchDNSMinderButton, InstallDNSMinderButton, UninstallDNSMinderButton)

End

Public Sub UninstallDNSMinderButton_Click()

  uninstall_app("dnsminder", DNSMinderSpinner, LaunchDNSMinderButton, InstallDNSMinderButton, UninstallDNSMinderButton)

End

Public Sub LaunchHostMinderButton_Click()

  launch_app("hostminder")

End

Public Sub InstallHostMinderButton_Click()

  install_app("hostminder", HostMinderSpinner, LaunchHostMinderButton, InstallHostMinderButton, UninstallHostMinderButton)

End

Public Sub UninstallHostMinderButton_Click()

  uninstall_app("hostminder", HostMinderSpinner, LaunchHostMinderButton, InstallHostMinderButton, UninstallHostMinderButton)

End

Public Sub LaunchParentalControlsButton_Click()

  launch_app("malcontent-control")

End

Public Sub InstallParentalControlsButton_Click()

  install_app("ubuntu-ce-parental-controls", ParentalControlsSpinner, LaunchParentalControlsButton, InstallParentalControlsButton, UninstallParentalControlsButton)

End

Public Sub UninstallParentalControlsButton_Click()

  uninstall_app("ubuntu-ce-parental-controls", ParentalControlsSpinner, LaunchParentalControlsButton, InstallParentalControlsButton, UninstallParentalControlsButton)

End

Public Sub LaunchTimekprButton_Click()

  launch_app("pkexec /usr/bin/timekpra", False, True)

End

Public Sub InstallTimekprButton_Click()

  install_app("timekpr-next", TimekprSpinner, LaunchTimekprButton, InstallTimekprButton, UninstallTimekprButton)

End

Public Sub UninstallTimekprButton_Click()

  uninstall_app("timekpr-next", TimekprSpinner, LaunchTimekprButton, InstallTimekprButton, UninstallTimekprButton)

End

Public Sub LaunchXiphosButton_Click()

  launch_app("xiphos")

End

Public Sub InstallXiphosButton_Click()

  install_app("xiphos sword-text-web sword-text-kjv", XiphosSpinner, LaunchXiphosButton, InstallXiphosButton, UninstallXiphosButton)

End

Public Sub UninstallXiphosButton_Click()

  uninstall_app("xiphos", XiphosSpinner, LaunchXiphosButton, InstallXiphosButton, UninstallXiphosButton)

End

Public Sub LaunchBibleTimeButton_Click()

  launch_app("bibletime")

End

Public Sub InstallBibleTimeButton_Click()

  install_app("bibletime sword-text-web sword-text-kjv", BibleTimeSpinner, LaunchBibleTimeButton, InstallBibleTimeButton, UninstallBibleTimeButton)

End

Public Sub UninstallBibleTimeButton_Click()

  uninstall_app("bibletime", BibleTimeSpinner, LaunchBibleTimeButton, InstallBibleTimeButton, UninstallBibleTimeButton)

End

Public Sub LaunchBibleditButton_Click()

  launch_app("bibledit")

End

Public Sub InstallBibleditButton_Click()

  install_app("bibledit", BibleditSpinner, LaunchBibleditButton, InstallBibleditButton, UninstallBibleditButton)

End

Public Sub UninstallBibleditButton_Click()

  uninstall_app("bibledit", BibleditSpinner, LaunchBibleditButton, InstallBibleditButton, UninstallBibleditButton)

End

Public Sub LaunchBibleKJVButton_Click()

  launch_app("bible", True)

End

Public Sub InstallBibleKJVButton_Click()

  install_app("bible-kjv", BibleKJVSpinner, LaunchBibleKJVButton, InstallBibleKJVButton, UninstallBibleKJVButton)

End

Public Sub UninstallBibleKJVButton_Click()

  uninstall_app("bible-kjv", BibleKJVSpinner, LaunchBibleKJVButton, InstallBibleKJVButton, UninstallBibleKJVButton)

End

Public Sub LaunchBollsButton_Click()

  launch_app("bolls")

End

Public Sub InstallBollsBibleButton_Click()

  install_app("bolls", BollsSpinner, LaunchBollsButton, InstallBollsBibleButton, UninstallBollsButton, True)

End

Public Sub UninstallBollsButton_Click()

  uninstall_app("bolls", BollsSpinner, LaunchBollsButton, InstallBollsBibleButton, UninstallBollsButton, True)

End

Public Sub ShowDetailsButton_Click()

  TerminalView.Show
  ShowDetailsButton.Hide
  HideDetailsButton.Show

End

Public Sub HideDetailsButton_Click()

  TerminalView.Hide
  HideDetailsButton.Hide
  ShowDetailsButton.Show

End

